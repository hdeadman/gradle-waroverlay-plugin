plugins {
    id 'net.researchgate.release' version '2.7.0'
    id 'signing'
}

apply plugin: "groovy"
apply plugin: "maven"

repositories {
    mavenLocal()
    mavenCentral()

    maven { url "https://raw.github.com/synergian/wagon-git/releases"}
}

ext {
    publishReleases = Boolean.getBoolean("publishReleases")
    bintrayUsername = System.getProperty("bintrayUsername")
    bintrayAPIKey = System.getProperty("bintrayAPIKey")
    githubUsername = System.getProperty("githubUsername")
    githubPassword = System.getProperty("githubPassword")
}

configurations {
    deployerJar
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    deployerJar "ar.com.synergian:wagon-git:0.3.0"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

def isArtifactSigningRequired = {
    return (publishReleases && bintrayUsername != null && bintrayAPIKey != null)
}

signing {
    required = isArtifactSigningRequired
    sign configurations.archives
}

createReleaseTag.dependsOn uploadArchives

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment)}
            configuration = configurations.deployerJar
            repository(url: "https://api.bintray.com/maven/scalding/maven/gradle-waroverlay-plugin") {
                authentication(userName: bintrayUsername, password: bintrayAPIKey)
            }
            snapshotRepository(url: "git:snapshots://git@bitbucket.org:scalding/maven-repo.git") {
                authentication(userName: githubUsername, password: githubPassword)
            }
            pom.project {
                name "Gradle WAR Overlay"
                packaging "jar"
                description "A gradle plugin to functionality similar to the war overlay function in maven."
                url "https://github.com/scalding/gradle-waroverlay-plugin"

                scm {
                    url "scm:git@github.com:scalding/gradle-waroverlay-plugin"
                    connection "scm:git@github.com:scalding/gradle-waroverlay-plugin"
                    developerConnection "scm:git@github.com:scalding/gradle-waroverlay-plugin"
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'scalding'
                        name 'Jonathan Johnson'
                    }
                    developer {
                        id 'dima'
                        name 'Dmitriy Kopylenko'
                    }
                }
            }
        }
    }
}

